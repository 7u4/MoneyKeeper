apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'io.fabric'

// 加载 keystore.properties 文件
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "me.bakumon.moneykeeper"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 31
        versionName "3.5.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // 指定room.schemaLocation生成的文件路径
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    // MaterialDateTimePicker 库用到了 java8 的特性
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        debug {
            storeFile file("key/debug_key")
            storePassword "123456"
            keyAlias "key"
            keyPassword "123456"
        }
        release {
            storeFile file(keystoreProperties['RELEASE_STORE_FILE'])
            storePassword keystoreProperties['RELEASE_KEYSTORE_PASSWORD']
            keyAlias keystoreProperties['RELEASE_KEY_ALIAS']
            keyPassword keystoreProperties['RELEASE_KEY_PASSWORD']
        }
    }
    buildTypes {
        debug {
            // 签名
            signingConfig signingConfigs.debug
            versionNameSuffix ".debug"
            applicationIdSuffix ".debug"
            resValue "string", "packageName", "me.bakumon.moneykeeper.debug"
        }
        release {
            // 签名
            signingConfig signingConfigs.release
            resValue "string", "packageName", "me.bakumon.moneykeeper"
            // Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            // 混淆
            minifyEnabled true
            proguardFile 'proguard-android.txt'
            proguardFile 'proguard-rules.pro'
        }
        android.applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "MoneyKeeper-${variant.baseName}-${variant.versionCode}-${variant.versionName}.apk"
            }
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1-alpha01'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1-alpha01'
    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // leakcanary -- 未使用 AndroidX
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.2'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.2'
    // appcompat
    implementation 'androidx.appcompat:appcompat:1.0.2'
    // design
    implementation 'com.google.android.material:material:1.1.0-alpha01'
    // preference
    implementation 'androidx.preference:preference:1.1.0-alpha02'
    // customtabs
    implementation 'androidx.browser:browser:1.0.0'
    // ViewModel and LiveData
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    kapt 'androidx.lifecycle:lifecycle-compiler:2.0.0'
    // Room
    implementation 'androidx.room:room-runtime:2.1.0-alpha03'
    implementation 'androidx.room:room-rxjava2:2.1.0-alpha03'
    kapt 'androidx.room:room-compiler:2.1.0-alpha03'
    // multitype
    implementation 'me.drakeet.multitype:multitype:3.5.0'
    implementation 'me.drakeet.multitype:multitype-ktx:3.5.0'
    // about
    implementation 'me.drakeet.support:about:2.2.0'
    implementation 'me.drakeet.support:about-extension:2.2.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.squareup.moshi:moshi:1.5.0'
    // flexbox
    implementation 'com.google.android:flexbox:1.1.0'
    // rx
    implementation 'io.reactivex.rxjava2:rxjava:2.1.3'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    // retrofit2
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    //
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    //
    implementation 'com.burgstaller:okhttp-digest:1.17'
    // materialdatetimepicker
    implementation 'com.wdullaer:materialdatetimepicker:4.1.0'
    // material-dialogs
    implementation 'com.afollestad.material-dialogs:core:2.0.0-rc3'
    implementation 'com.afollestad.material-dialogs:files:2.0.0-rc3'
    // permission -- 未使用 AndroidX
    implementation 'com.yanzhenjie:permission:2.0.0-rc12'
    // 指纹
    implementation 'com.wei.android.lib:fingerprintidentify:1.2.6'
    // MPChart
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0-alpha'
    // fabric
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.4@aar', { transitive = true }
    // 文件操作
    implementation 'com.snatik:storage:2.1.0'
    // 时间格式化
    implementation 'org.ocpsoft.prettytime:prettytime:4.0.1.Final'
    // 重启进程
    implementation 'com.jakewharton:process-phoenix:2.0.0'
}
